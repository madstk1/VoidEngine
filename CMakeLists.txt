##
## PREAMBLE
##

CMAKE_MINIMUM_REQUIRED(VERSION 3.11)
FIND_PACKAGE(PkgConfig REQUIRED)

SET(VOID_PROJECT_VERSION 0.1.0)
SET(VOID_PROJECT_DESC "A dumb-ass game engine.")
SET(VOID_PROJECT_LANG C CXX)

SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -Wall")
SET(CMAKE_C_EXTENSIONS ON)
SET(CMAKE_CXX_STANDARD 11)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
SET(CMAKE_FIND_LIBRARY_SUFFIXES .a)

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/VoidEngine)

##
## BUILD OPTIONS
##

OPTION(VOID_ENABLE_OPENGL   "Enable OpenGL backend"              TRUE)

IF(NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
    MESSAGE(STATUS "[VOID] Setting build type to 'Debug' as none was specified.")
    SET(CMAKE_BUILD_TYPE "Debug" CACHE STRING "Choose the type of build." FORCE)
ENDIF()

MESSAGE(STATUS "[VOID] Building library as ${CMAKE_BUILD_TYPE}")
MESSAGE(STATUS "[VOID] Building with OpenGL backend: ${VOID_ENABLE_OPENGL}")

##
## PROJECT
##

PROJECT(
    "void"
    VERSION     ${VOID_PROJECT_VERSION}
    DESCRIPTION ${VOID_PROJECT_DESC}
    LANGUAGES   ${VOID_PROJECT_LANG}
)

##
## GLAD OPTIONS
##

SET(GLAD_PROFILE    "core"   CACHE STRING "" FORCE)
SET(GLAD_API        "gl=4.5" CACHE STRING "" FORCE)
SET(GLAD_GENERATOR  "c"      CACHE STRING "" FORCE)
SET(GLAD_SPEC       "gl"     CACHE STRING "" FORCE)
SET(GLAD_REPRODUCABLE ON     CACHE BOOL   "" FORCE)
SET(GLAD_EXTENSIONS 
    "GL_ARB_debug_output"
    CACHE STRING "" FORCE)

ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/glad/)

##
## GLFW
##

FIND_PACKAGE(glfw3 QUIET)
IF(NOT glfw3_FOUND)
    MESSAGE(STATUS "[VOID] Didn't find GLFW on system. Using in-tree build.")

    SET(BUILD_SHARED_LIBS   OFF CACHE BOOL "" FORCE)
    SET(GLFW_BUILD_DOCS     OFF CACHE BOOL "" FORCE)
    SET(GLFW_BUILD_TESTS    OFF CACHE BOOL "" FORCE)
    SET(GLFW_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
    SET(GLFW_INSTALL        OFF CACHE BOOL "" FORCE)
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/glfw/)
ENDIF()

##
## GLM
##

FIND_PACKAGE(glm QUIET)
IF(NOT glm_FOUND)
    MESSAGE(STATUS "[VOID] Didn't find GLM on system. Using in-tree build.")

    SET(GLM_TEST_ENABLE     OFF CACHE BOOL "" FORCE)
    SET(BUILD_SHARED_LIBS   OFF CACHE BOOL "" FORCE)
    SET(BUILD_STATIC_LIBS   ON  CACHE BOOL "" FORCE)
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/glm/)
ENDIF()

##
## ASSIMP
##

PKG_SEARCH_MODULE(ASSIMP QUIET assimp)

IF(NOT ASSIMP_MODULE_NAME STREQUAL "assimp")
    MESSAGE(STATUS "[VOID] Didn't find Assimp on system. Using in-tree build.")

    SET(BUILD_SHARED_LIBS         OFF CACHE BOOL "" FORCE)
    SET(ASSIMP_BUILD_TESTS        OFF CACHE BOOL "" FORCE)
    SET(ASSIMP_INSTALL            OFF CACHE BOOL "" FORCE)
    SET(ASSIMP_NO_EXPORT          ON  CACHE BOOL "" FORCE)
    SET(ASSIMP_BUILD_ASSIMP_TOOLS OFF CACHE BOOL "" FORCE)
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/assimp/)
    INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/assimp/include)
ENDIF()

##
## GLSLANG
##

FIND_PACKAGE(glslang QUIET)

IF(NOT glslang_FOUND)
    MESSAGE(STATUS "[VOID] Didn't find glslang on system. Using in-tree build.")

    SET(BUILD_SHARED_LIBS       OFF CACHE BOOL "" FORCE)
    SET(ENABLE_GLSLANG_BINARIES OFF CACHE BOOL "" FORCE)
    SET(ENABLE_SPVREMAPPER      OFF CACHE BOOL "" FORCE)
    SET(SKIP_GLSLANG_INSTALL    ON  CACHE BOOL "" FORCE)
    SET(ENABLE_HLSL             OFF CACHE BOOL "" FORCE)
    SET(ENABLE_CTEST            OFF CACHE BOOL "" FORCE)
    SET(ENABLE_PCH              OFF CACHE BOOL "" FORCE)
    SET(ENABLE_OPT              OFF CACHE BOOL "" FORCE)
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/glslang)
    INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/glslang)
ENDIF()

##
## SPIRV-CROSS
##

FIND_PACKAGE(spirv_cross_core QUIET)
IF(NOT spirv_cross_core_FOUND)
    MESSAGE(STATUS "[VOID] Didn't find SPIRV-Cross on system. Using in-tree build.")

    SET(SPIRV_CROSS_STATIC      ON  CACHE BOOL "" FORCE)
    SET(SPIRV_CROSS_SHARED      OFF CACHE BOOL "" FORCE)
    SET(SPIRV_CROSS_CLI         OFF CACHE BOOL "" FORCE)
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/SPIRV)
    INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/SPIRV/SPIRV-Cross/include)
ENDIF()

##
## VOID ENGINE
##

ADD_SUBDIRECTORY(VoidEngine)
